set shell := ["bash", "-c"]
set dotenv-load := true
set quiet := true

APP_FQDN := "worker-metaframe.localhost"
APP_PORT := env_var_or_default("APP_PORT", "443")
normal := '\033[0m'
green := "\\e[32m"

@_help:
    just --list --unsorted --list-heading $'Commands: (all services)\n'
    echo -e ""
    echo -e "    Sub-commands (e.g. just browser dev):"
    echo -e "       {{ green }}browser{{ normal }}       -> just browser"
    echo -e "       {{ green }}worker{{ normal }}        -> just worker"
    echo -e "       {{ green }}api{{ normal }}           -> just api"

# Validate mode
@_validate_mode mode="":
    @if [ "{{ mode }}" = "remote" ] || [ "{{ mode }}" = "local" ]; then :; else echo "Error: Mode must be 'remote' or 'local'" >&2; exit 1; fi

# Develop: open the browser and start the dev stack
@dev mode="remote" +args="": (_validate_mode mode) _ensure-all open (_up mode args)

# Start the docker compose local stack
@_up mode="remote" +args="": (_validate_mode mode) _ensure-all _mkcert
    #!/usr/bin/env bash
    # A local NPM registry saves time, bandwidth, and avoids rate limits
    # If a local npm registry cache is running, use that for all npm modules (faster and better when internet not great)
    # docker run -d -v verdaccio_npm_storage:/verdaccio/storage --name verdaccio -p 4873:4873 verdaccio/verdaccio
    # https://verdaccio.org/docs/docker/
    STATUS_CODE=$(curl --max-time 2 --write-out '%{http_code}' --silent --output /dev/null http://localhost:4873)
    if [ $STATUS_CODE = "200" ]; then
        export NPM_CONFIG_REGISTRY=http://host.docker.internal:4873
    fi
    docker compose --project-name=dev -f docker-compose.yml -f docker-compose-{{ mode }}.yml rm --force 2>/dev/null || true
    docker rm dev-worker-1 2>/dev/null || true
    docker compose --project-name=dev -f docker-compose.yml -f docker-compose-{{ mode }}.yml up --remove-orphans {{ args }}

# Shut down the local stack: 'docker compose down'
@down mode="remote" +args="": (_validate_mode mode)
    #!/usr/bin/env bash
    docker compose --project-name=dev -f docker-compose.yml -f docker-compose-{{ mode }}.yml down {{ args }}

# Clean up the project
@clean mode="remote" +args="": (_validate_mode mode)
    #!/usr/bin/env bash
    docker compose --project-name=dev -f docker-compose.yml -f docker-compose-{{ mode }}.yml down -v
    just browser/clean
    rm -rf .cache

# Open the browser to the local frontend
@open: _ensure-all
    deno run --allow-all https://deno.land/x/metapages@v0.0.27/exec/open_url.ts 'https://metapages.github.io/load-page-when-available/?url=https://{{ APP_FQDN }}:{{ APP_PORT }}#?job=JTdCJTIyY29tbWFuZCUyMiUzQSUyMmxzJTIwLWxhJTIyJTJDJTIyaW1hZ2UlMjIlM0ElMjJhbHBpbmUlM0EzLjE4LjUlMjIlN0Q=&queue=local1' || true

# Quick compilation checks
@check: _ensure-all
    echo -n "worker check..."
    just worker/check
    echo -n "api check..."
    just api/check
    echo -n "browser check..."
    just browser/check
    echo -n "test check..."
    just test/check
    echo -n "shared check..."
    just shared/check
    echo -n "cli check..."
    just cli/check

# Format all supported files
@fmt +args="":
    deno fmt {{ args }}
    find . -name justfile -exec just --fmt --unstable -f {} {{ args }} \;

status queue="local1":
    curl https://{{ APP_FQDN }}:{{ APP_PORT }}/{{ queue }}/status | jq .

status-test queue="local1":
    APP_PORT=4470 curl https://{{ APP_FQDN }}:4470/{{ queue }}/status | jq .

status-prod queue="public1":
    curl https://container.mtfm.io/{{ queue }}/status | jq .

test: check
    #!/usr/bin/env bash
    set -e # exit when any command fails, preserving the status of the failed command

    cleanup() {
        docker compose --project-name=test -f docker-compose.yml -f docker-compose-remote.yml down 2>/dev/null || true
    }

    trap cleanup EXIT # cleanup when the script exits for any reason, including failure

    cleanup

    # Generate a random port between 10000-65535 and ensure it's not in use
    get_free_port() {
        while true; do
            local port=$(shuf -i 10000-65535 -n 1)
            # Check if port is in use using netstat or ss
            # Check if port is in use using netstat/ss on Linux or lsof on macOS
            if [ "$(uname)" == "Linux" ]; then
                if ! ss -tuln | grep -q ":${port} "; then
                    echo "${port}"
                    break
                fi
            else
                if ! lsof -i ":${port}" > /dev/null 2>&1; then
                    echo "${port}"
                    break
                fi
            fi
        done
    }

    export APP_PORT=$(get_free_port)
    echo -e "Using port ${APP_PORT}"
    # Get host IP address for Linux
    if [ "$(uname)" == "Linux" ]; then
        HOST_IP=$(ip -4 addr show docker0 | grep -Po 'inet \K[\d.]+')
    else
        HOST_IP="host.docker.internal"
    fi

    # A local NPM registry saves time, bandwidth, and avoids rate limits
    # This fails on specific npm modules, so we'll just not use it for now
    # STATUS_CODE=$(curl --write-out '%{http_code}' --silent --output /dev/null http://localhost:4873 || true)
    # if [ $STATUS_CODE = "200" ]; then
    #     export NPM_CONFIG_REGISTRY=http://${HOST_IP}:4873
    # fi
    # This is causing currently unknown issues, a dangling worker 
    docker rm test-worker-1 2>/dev/null || true
    docker compose --project-name=test -f docker-compose.yml -f docker-compose-remote.yml up --build --remove-orphans --detach
    just worker/test
    just test/test
    just shared/test
    just cli/test

# Clean up the project
@test-cleanup:
    docker compose --project-name=test -f docker-compose.yml -f docker-compose-remote.yml down || true

# Publish e.g. docker images with whatever versioning scheme is appropriate
@publish-versioned-artifacts version="":
    just worker/publish-docker-images {{ version }}

# DEV: generate TLS certs for HTTPS over localhost https://blog.filippo.io/mkcert-valid-https-certificates-for-localhost/
_mkcert: _ensure_mkcert
    #!/usr/bin/env bash
    rm -rf .cache/traefik/certs
    mkdir -p .cache/traefik/certs/ ;
    mkcert -cert-file .cache/traefik/certs/local-cert.pem -key-file .cache/traefik/certs/local-key.pem {{ APP_FQDN }} minio.{{ APP_FQDN }} localhost ;
    if grep -q "{{ APP_FQDN }}" /etc/hosts; then
        echo -e "âœ… Hostname {{ APP_FQDN }} found in /etc/hosts"
    else
        echo -e "ðŸ’¥ Hostname {{ APP_FQDN }} not found in /etc/hosts"
        echo -e "ðŸ‘‰ Add: '127.0.0.1   {{ APP_FQDN }}' to /etc/hosts"
        exit 1
    fi
    if grep -q "minio.{{ APP_FQDN }}" /etc/hosts; then
        echo -e "âœ… Hostname minio.{{ APP_FQDN }} found in /etc/hosts"
    else
        echo -e "ðŸ’¥ Hostname minio.{{ APP_FQDN }} not found in /etc/hosts"
        echo -e "ðŸ‘‰ Add: '127.0.0.1   minio.{{ APP_FQDN }}' to /etc/hosts"
        exit 1
    fi
    # if [ ! -f .cache/traefik/certs/local-key.pem ]; then \
    #     FQDN={{ APP_FQDN }} CERT_NAME=local CERTS_DIR=.cache/traefik/certs deno run --allow-all https://deno.land/x/metapages@v0.0.27/commands/ensure_mkcert.ts ;\
    # fi

@_ensure-all: _ensure_docker _ensure_deno

@_ensure_docker:
    if ! command -v docker > /dev/null; then \
        echo -e "\nDocker is not installed. Please install Docker and try again: ðŸ‘‰ {{ green }}https://docs.docker.com/engine/install/{{ normal }}\n"; \
        exit 1; \
    fi

@_ensure_deno:
    if ! command -v deno > /dev/null; then \
        echo -e "\nDeno is not installed. Please install Deno and try again: ðŸ‘‰ {{ green }}https://docs.deno.com/runtime/manual/getting_started/installation{{ normal }}\n"; \
        exit 1; \
    fi

@_ensure_mkcert:
    if ! command -v mkcert > /dev/null; then \
        echo -e "\nmkcert is not installed. Please install mkcert and try again: ðŸ‘‰ {{ green }}https://github.com/FiloSottile/mkcert?tab=readme-ov-file#installation{{ normal }}\n"; \
        exit 1; \
    fi

# app subdirectory commands

alias browser := _browser

@_browser +args="":
    just browser/{{ args }}

# app subdirectory commands

alias worker := _worker

@_worker +args="":
    just worker/{{ args }}

# app subdirectory commands

alias api := _api

@_api +args="":
    just api/{{ args }}
