set shell := ["bash", "-c"]
set dotenv-load := true
set export := true

APP_PORT := env_var_or_default("APP_PORT", "443")
API_URL := "https://worker-metaframe.localhost:" + APP_PORT
normal := '\033[0m'
green := "\\e[32m"

@_help:
    just --list --unsorted --list-heading $'Testing commands:\n'

# Quick type check
@check:
    deno check src
    echo "✅ tests compilation"

@watch +args="":
    deno test --unsafely-ignore-certificate-errors --watch --allow-all --unstable-broadcast-channel --unstable-kv src/{{ args }}

@test +args="":
    deno test --unsafely-ignore-certificate-errors --allow-all --unstable-broadcast-channel --unstable-kv src/{{ args }}
    echo "✅ functional tests"

@test-in-docker stack="test" +args="":
    cd .. && \
    APP_PORT={{ APP_PORT }} docker compose --project-name={{ stack }} -f docker-compose.yml -f docker-compose-remote.yml -f docker-compose-test.yml run --no-deps --rm test deno test --unsafely-ignore-certificate-errors --allow-all src/{{ args }}
