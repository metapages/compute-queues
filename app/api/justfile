# Always use locally installed tools (always pin versions)

set shell := ["bash", "-c"]
set dotenv-load := true
set export := true

DENO_DEPLOY_TOKEN := env_var_or_default("DENO_DEPLOY_TOKEN", "")

# Make sure to include the just and deployctl paths here

PATH := env_var_or_default("PATH", "") + ":" + env_var_or_default("HOME", "") + "/.deno/bin:/snap/bin"

# minimal formatting, bold is very useful

bold := '\033[1m'
normal := '\033[0m'

@_help:
    just --list --unsorted --list-heading $'Api commands:\n'

# Run the server locally
dev +args="": _build_shared
    cd ../../ && \
    deno run --watch --allow-all --unstable-kv --unstable-broadcast-channel --unstable-cron app/api/src/server.ts {{ args }}

# Quick compile checks
@check: _build_shared
    deno check src/server.ts
    echo "✅ api typescript compilation"

# Lint
@lint:
    deno lint

# Lint and fix issues
@lint-fix:
    deno lint --fix

# Format
@fmt:
    deno fmt

# Format check
@fmt-check:
    deno fmt --check

# Fix
@fix: lint-fix fmt

# Run CI
@ci: fmt-check lint

# Perform a deploy to https://dash.deno.com/projects/compute-queue-api
@build: check
    just ../browser/build

# Build, then run the server locally. TO skip the build: 'just _serve'
serve: build _serve

_serve:
    cd ../../ && \
    PORT=3073 \
    DENO_DIR=.deno \
    DENO_INSTALL_ROOT=.deno \
    AWS_ACCESS_KEY_ID=minioaccesskey123123 \
    AWS_SECRET_ACCESS_KEY=minioaccesskey123123 \
    AWS_REGION=us-west-2 \
    AWS_ENDPOINT=http://minio:9000 \
    AWS_S3_BUCKET=localbucket \
    DENO_KV_ACCESS_TOKEN=localdenoaccesstoken \
    DENO_KV_URL=http://denokv:4512 \
        deno run --allow-sys --allow-net --allow-read --allow-env --unstable-kv --unstable-cron app/api/src/server.ts

# Perform a fresh build and deploy to deno-deploy
deploy: _ensure_deployctl build
    cd ../../ && \
        deployctl deploy \
        --project=compute-queue-api \
        --prod \
        --include=app/api/deno.json \
        --include=app/api/src \
        --include=app/browser/dist \
        --include=app/cli/deno.json \
        --include=app/shared/deno.json \
        --include=app/shared/src \
        --include=app/test/deno.json \
        --include=app/worker/deno.json \
        --include=deno.json \
        --include=deno.lock \
        app/api/src/server.ts

# Delete generate assets and caches
clean:
    rm -rf dist

@_ensure_deployctl:
    if ! command -v deployctl &> /dev/null; then echo '‼️ deployctl is being installed ‼️'; deno install -Arf jsr:@deno/deployctl; fi

# Add new helper recipe for building shared dependencies
_build_shared:
    #!/usr/bin/env bash
    set -eo pipefail
    cd ../shared && just build
