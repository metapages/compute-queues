# Always use locally installed tools (always pin versions)
set shell             := ["bash", "-c"]
set dotenv-load       := true
set export            := true
DIST                  := "/tmp/worker-cli"

# minimal formatting, bold is very useful
bold                  := '\033[1m'
normal                := '\033[0m'

@_help:
  just --list --unsorted --list-heading $'Commands:\n'
  echo -e ""
  echo -e "   Run the cli anywhere:"
  echo -e "       deno run -A --unstable jsr:@metapages/cli@$(cat deno.json | jq -r '.version')"
  

# Run the cli, pointing to the local API
dev +args="":
  deno run --unsafely-ignore-certificate-errors --location https://worker-metaframe.localhost --allow-all src/cli.ts {{args}}

# Run the cli, pointing to production API
prod +args="":
  deno run --location https://container.mtfm.io --allow-all src/cli.ts {{args}}

# Publish the cli to JSR
@publish:
  rm -rf {{DIST}}
  mkdir -p {{DIST}}
  cp -r src {{DIST}}
  cp deno.json {{DIST}}
  cp LICENSE {{DIST}}
  cd {{DIST}} && deno publish --unstable

compile:
  deno compile --location https://worker-metaframe.localhost --allow-net --allow-read --allow-write --output dist/mtpg src/cli.ts

# Quick compile checks
@check:
  deno check src/cli.ts
  echo "✅ cli typescript compilation"

@test:
  echo "❗ no cli tests yet"